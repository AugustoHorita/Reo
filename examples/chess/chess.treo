import reo.sync;
import reo.binop;
import reo.fifo1;
import reo.transformer;
import reo.syncdrain;
import reo.filter;
import reo.fifofull;

chess = (){

	{
	Team<T,T>(whiteI,whiteR[1..T],whiteC[1..T],whiteM,whiteO)
	fifo1(whiteO,blackI)
	Team<1,1>(blackI,blackR,blackC,blackM,blackO)
	fifofull<"">(blackO,whiteI)
	
	{
		{ Engine(whiteC[i],whiteR[i])  | i:<1..T> }
		Engine(blackC,blackR)
	|
	
		Engine=(a?,b!){
			#PR
			identity(a;b)
		|
			Java:"runtime.chess.Workers.Engine"
		}
	}
	
	{ Display(whiteM,blackM)
	|
	  Display(a?,b?){
		#PR
		identity(a;b)
	  |
		Java:"runtime.chess.Workers.Display"

	  }
	}
	|

	T=3
	}
	
}

Team<T:int,P:int>(a?,y[1..T]?,hist[1..P]!,move!,out!){
        fifo1(a,h)

        { sync(a,hist[i]) | i:<1..P> }

        { transformer<PARSE>(y[j],Q[j]) | j : <1..T>, PARSE = "runtime.chess.Functions.parse"}

        concatenator(Q[1..T],p)
        {transformer<MAJORITY>(p,d) | MAJORITY = "runtime.chess.Functions.majority" }
        syncdrain(d,h)
        { filter<MOVE>(d,e) | MOVE = "runtime.chess.Relations.Move" }
        sync(e,move)
        { binop<CONCATENATE>(h,e,out) | CONCATENATE = "runtime.chess.Functions.concatenate" }

}

concatenator(a[1..N]?,b!){
        { sync(a[1],b) | N=1}

        { sync(a[1],P[1])
                { binop<CONCATENATE>(a[i+1],P[i],P[i+1])
                |
                i:<1..(N-1)>,
                CONCATENATE = "runtime.chess.Functions.concatenate"
                }

          sync(P[N],b)
        |
          N>1}
}

